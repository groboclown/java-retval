plugins {
    id 'java-library'
    id 'checkstyle'
    id 'jacoco'
}

version = '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    api 'com.google.code.findbugs:jsr305:3.0.2'

    // implementation 'com.google.code.findbugs:jsr305:3.0.2'

    // Use JUnit Jupiter for testing.
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
}

test {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}
jacocoTestReport {
    dependsOn test
}
jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            limit {
                // Is there a way to also raise branch coverage?
                minimum = 1.0
            }
        }
    }
}
check {
    // Add the coverage verification to the check, rather than test, target,
    // so that IDE and other cases of one-off test executions doesn't cause superfluous
    // errors unrelated to just test pass/fail.
    finalizedBy jacocoTestCoverageVerification
}
checkstyle {
    toolVersion "9.0"
}


task zipJavadoc(type: Zip) {
    dependsOn javadoc

    archiveClassifier = 'javadoc'

    from javadoc.destinationDir
}

task zipSource(type: Zip) {
    archiveClassifier = 'source'

    from("${projectDir}/src/main/java")
    from("${projectDir}/src/main/resources")
}

assemble {
    dependsOn zipJavadoc, zipSource
}
