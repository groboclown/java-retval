plugins {
    id 'java-library'
    id 'checkstyle'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
}

version = '2.0.0'
group = 'net.groboclown'


repositories {
    mavenCentral()
}

dependencies {
    api 'com.google.code.findbugs:jsr305:3.0.2'

    // implementation 'com.google.code.findbugs:jsr305:3.0.2'

    // Use JUnit Jupiter for testing.
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
}

test {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}
jacocoTestReport {
    dependsOn test
}
jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            limit {
                // Is there a way to also raise branch coverage?
                minimum = 1.0
            }
        }
    }
}
check {
    // Add the coverage verification to the check, rather than test, target,
    // so that IDE and other cases of one-off test executions doesn't cause superfluous
    // errors unrelated to just test pass/fail.
    finalizedBy jacocoTestCoverageVerification
}
checkstyle {
    toolVersion "9.0"
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options.addBooleanOption('html5', true)
}


publishing {
    publications {
        official(MavenPublication) {
            from components.java

            pom {
                name = 'retval'
                description = 'Combine error messages and return values in a single return object, without using Exceptions.'
                url = "https://groboclown.github.io/java-retval/"
                licenses {
                    license {
                        name = "MIT License"
                        url = "https://mit-license.org/"
                    }
                }
                developers {
                    developer {
                        id = "groboclown"
                        name = "Groboclown"
                        email = "matt@groboclown.com"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/groboclown/java-retval.git"
                    developerConnection = "scm:git:git@github.com:groboclown/java-retval.git"
                    url = "https://github.com/groboclown/java-retval"
                }
            }
        }
    }
    repositories {
        maven {
            setUrl("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
            authentication {
                basic(BasicAuthentication)
            }
            credentials(PasswordCredentials) {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}


signing {
    // See
    // https://github.com/gradle/gradle/issues/16597
    useGpgCmd()
    sign publishing.publications.official
}
